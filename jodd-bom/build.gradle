
ext.moduleName = 'jodd-bom'
ext.moduleDescription = "Jodd (Bill of Materials)"

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: "${gradleScriptDir}/publish-maven.gradle"

group = 'org.jodd'

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: classes) {
	classifier = 'javadoc'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			buildBomDependencies(pom)
		}
	}
}

def buildBomDependencies(pom) {
	pom.withXml {
		asNode().children().last() + {
			delegate.dependencyManagement {
				delegate.dependencies {
					parent.javaModules().sort { "$it.name" }.each { p ->
						delegate.dependency {
							delegate.groupId(p.group)
							delegate.artifactId(p.name)
							delegate.version(p.version)
						}
					}
				}
			}
		}
	}
}