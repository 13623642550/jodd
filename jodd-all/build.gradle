
ext.moduleName = 'Jodd Bundle'
ext.moduleDescription = 'Jodd bundle - all classes in one jar'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: "${gradleScriptDir}/provided.gradle"
apply from: "${gradleScriptDir}/publish-maven.gradle"

group = 'org.jodd'

dependencies {
    compile javaBundleModules()
}

jar {
    baseName = 'jodd-all'
    javaBundleModules().each { subproject ->
        from { subproject.sourceSets.main.output }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from javaBundleModules().sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: parent.javadocAll) {
    classifier = 'javadoc'
    //from javaBundleModules().javadoc.destinationDir
    from "${parent.buildDir}/reports/javadoc"
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}
